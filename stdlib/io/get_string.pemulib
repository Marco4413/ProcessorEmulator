
;;
 ; @File
 ; @Author: [hds536jhmk](https://github.com/hds536jhmk)
 ; @Name: get_string.pemulib
 ; @Description: A PEMU Library that adds the f_get_string function
 ; @License: MIT
 ; @Created-At   : 20-07-2021
 ; @Last-Modified: 21-07-2021

#INCLUDE "../base.pemulib"
#INCLUDE "../timings/sleep.pemulib"
#INCLUDE "keys.pemulib"
#INCLUDE "print.pemulib"

;;
 ; @Function
 ; @Name: get_string
 ; @Description: Gets a string from the user
 ; @Arguments
 ; @label { Pointer_String }: The String to show before the user's input
 ; @ptr { Pointer_Any }: The Pointer to the buffer holding the new String
 ; @max_length { Number }: The Length of the buffer holding the String or the max length of the String
#DA {
    _get_string_max_length: 0
    _get_string_length: 0
    _get_string_key:    0
    _get_string_char:   0
}
f_get_string:
    POP stdlib_temp
    POP _get_string_max_length
    POP _get_string_ptr
    POP _get_string_label
    PUSH stdlib_temp

    DATA _get_string_length @STDLIB_ZERO

    JMP _get_string_loop_display
    _get_string_loop:
        PUSH stdlib_delay_between_key_presses
        CALL f_sleep

        GETK _get_string_key
        GETC _get_string_char

        CMP _get_string_key stdlib_vk_undefined
        JE _get_string_loop

        CMP _get_string_key stdlib_key_confirm
        JE _get_string_end

        ; Calculating the pointer to the current character
        DATA stdlib_temp _get_string_ptr: 0
        ADD stdlib_temp _get_string_length

        CMP _get_string_key stdlib_key_delete
        JNE _get_string_loop_valid_char

        CMP _get_string_length stdlib_zero
        JBE _get_string_loop

        DEC stdlib_temp
        XMOV stdlib_temp stdlib_zero @STDLIB_XMOV_SRC_TO_REF_DST
        DEC _get_string_length
        JMP _get_string_loop_display

    _get_string_loop_valid_char:
        CMP _get_string_char stdlib_zero
        JE _get_string_loop

    _get_string_loop_add_char:
        CMP _get_string_length _get_string_max_length
        JAE _get_string_loop

        XMOV stdlib_temp _get_string_char @STDLIB_XMOV_SRC_TO_REF_DST
        INC _get_string_length

    _get_string_loop_display:
        OUTC stdlib_null
        ; If an input label was specified then print it
        CMP _get_string_label: 0 stdlib_null
        JE _get_string_loop_display_string
        PUSH _get_string_label
        CALL f_print
    _get_string_loop_display_string:
        PUSH _get_string_ptr
        CALL f_print
    JMP _get_string_loop

_get_string_end:
    RET
